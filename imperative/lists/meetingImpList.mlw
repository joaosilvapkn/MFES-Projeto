module MeetingAbs


  use int.Int
  use list.List
  use set.Fset


  type user
  type meeting

  type stateT = abstract { mutable host : fset (meeting,user);
                           mutable participants : fset (meeting, user);
                           mutable active : fset meeting
                         }
                         
  invariant{forall u1 u2:user, m:meeting. (mem (m,u1) host /\ mem (m,u2) host) -> u1=u2}      

  invariant{forall u:user,m:meeting. (mem m active /\ mem (m,u) host) -> mem (m,u) participants}
           
  by { host = Fset.empty ; participants = Fset.empty; active = Fset.empty }
            
           

  val schedule (m: meeting) (h:user) (state:stateT) : ()
    writes{state.host}
    requires{forall u:user. not mem (m,u) state.host}
    requires{not mem m state.active}
    ensures{state.host = add (m,h) (old state.host)}
    ensures{state.active = old state.active}
    ensures{state.participants = old state.participants}


    
  val start (m : meeting) (state:stateT) : ()
    writes{state.active, state.participants}
    requires{not mem m state.active}
    requires{exists u:user. mem (m,u) state.host} 
    requires{forall u:user. not mem (m,u) state.participants}
    ensures{state.host= old state.host}
    ensures{state.active = Fset.add m (old state.active)}
    ensures{forall u:user. (Fset.mem (m,u) (old state.host)) <-> state.participants =  Fset.add (m,u) (old state.participants)} 


  val join (m:meeting) (u:user) (state:stateT) : ()
    writes{state.participants}
    requires{mem m state.active}
    requires{not mem (m,u) state.participants}
    ensures{state.participants = Fset.add (m,u) (old state.participants)}
    ensures{state.host = old state.host}
    ensures{state.active = old state.active}

   
  val leave (m:meeting) (u:user) (state:stateT) : ()
    writes{state.participants}
    requires{not mem (m,u) state.host}
    requires{mem m state.active}
    requires{mem (m,u) state.participants} 
    ensures{state.participants = Fset.remove (m,u) (old state.participants)}
    ensures{state.host = old state.host}
    ensures{state.active = old state.active}


  val end_ (m : meeting) (state:stateT) : ()
    writes{state.active, state.participants}
    requires{mem m state.active}
    ensures{state.host = old state.host}
    ensures{state.active = remove m (old state.active)}
    ensures{forall u:user, m2:meeting. m<>m2 -> (Fset.mem (m2,u) (old state.participants) <-> Fset.mem (m2,u) state.participants)}
    ensures{forall u:user. not Fset.mem (m,u) state.participants}
    


end



module MeetingImpList



  use int.Int
  use list.List
  use list.Mem
  use list.NumOcc
  use set.Fset
  use list.Elements




  type user
  type meeting

  val eq_user (x y :user) : bool
    ensures { result <-> x=y }
    
  val eq_meeting (x y :meeting) : bool
    ensures { result <-> x=y }

  type stateT = { mutable ghost host : fset (meeting,user);
                  mutable host_ : list (meeting,user);
                  mutable ghost participants : fset (meeting, user);
                  mutable participants_ : list (meeting, user);
                  mutable ghost active : fset meeting;
                  mutable active_ : list meeting
                }
   
  (* inv abs *)         
  invariant{forall u1 u2:user, m:meeting. (Fset.mem (m,u1) host /\ Fset.mem (m,u2) host) -> u1=u2}      
  invariant{forall u:user,m:meeting. (Fset.mem m active /\ Fset.mem (m,u) host) -> Fset.mem (m,u) participants}
           
  (* inv relacional *)
  invariant{forall u:user, m:meeting. Fset.mem (m,u) host <-> Mem.mem (m,u) host_}
  invariant{forall u:user, m:meeting. Fset.mem (m,u) participants <-> Mem.mem (m,u) participants_}
  invariant{forall m:meeting. Fset.mem m active <-> Mem.mem m active_}

  (* inv implementacional *)
  invariant{forall u:user, m:meeting. num_occ (m,u) host_ <=1}
  invariant{forall u:user, m:meeting. num_occ (m,u) participants_ <=1}
  invariant{forall m:meeting. num_occ m active_ <=1}
           
  by { host = Fset.empty ; participants = Fset.empty; active = Fset.empty;
       host_=Nil; participants_=Nil; active_=Nil }



  let mkMeeting () : stateT
  = { host_ = Nil ;
      active_ = Nil ; 
      participants_ = Nil;
      host = Fset.empty;
      active = Fset.empty;
      participants = Fset.empty
    }
    
    




    
  let schedule (m:meeting) (h:user) (state:stateT) : ()
    writes{state.host, state.host_}
    requires{forall u:user. not Fset.mem (m,u) state.host}
    requires{not Fset.mem m state.active}
    ensures{state.host = add (m,h) (old state.host)}
    ensures{state.active = old state.active}
    ensures{state.participants = old state.participants}
    = 
    state.host <- Fset.add (m,h) state.host;
    state.host_ <- Cons (m,h) state.host_
      
  
  
   
  let rec function getHost_(m : meeting) (l:list (meeting,user)) : user
    requires{exists u:user. Mem.mem (m,u) l}
    ensures{Mem.mem (m,result) l}
    variant{l}
    =
    match l with
    | Nil -> absurd
    | Cons (m2,u) t -> if eq_meeting m2 m then u else getHost_ m t
  end

   
  let start (m : meeting) (state:stateT) : ()
    writes{state.participants, state.participants_, state.active, state.active_}
    requires{not Fset.mem m state.active}
    requires{exists u:user. Fset.mem (m,u) state.host}
    requires{forall u:user. not Fset.mem (m,u) state.participants}
    ensures{state.host= old state.host}
    ensures{state.active = Fset.add m (old state.active)}
    ensures{forall u:user. (Fset.mem (m,u) (old state.host)) <-> state.participants =  Fset.add (m,u) (old state.participants)} 
  = 
    state.participants <- Fset.add (m, getHost_ m state.host_) state.participants;
    state.participants_ <- Cons (m, getHost_ m state.host_) state.participants_;  
    state.active <- Fset.add m state.active;
    state.active_ <- Cons m state.active_
    
    
 

  let join (m:meeting) (u:user) (state:stateT) : ()
    writes{state.participants, state.participants_}
    requires{Fset.mem m state.active}
    requires{not Fset.mem (m,u) state.participants}
    ensures{state.participants = Fset.add (m,u) (old state.participants)}
    ensures{state.host = old state.host}
    ensures{state.active = old state.active}
  = 
    state.participants <- Fset.add (m, u) state.participants;
    state.participants_ <- Cons (m, u) state.participants_ 

    

  
    
  let rec function leaveMeet_(m : meeting) (u:user) (l:list (meeting,user)) : list (meeting,user)
    requires{Mem.mem (m,u) l}
    requires{num_occ (m,u) l =1} 
    ensures{not Mem.mem (m,u) result}
    ensures{forall u2:user, m2:meeting. (m2<>m \/ u2<>u) -> (num_occ (m2,u2) l = num_occ (m2,u2) result)}
    variant{l}
    =
    match l with
    | Nil -> Nil
    | Cons (m2,u2) t -> if eq_meeting m2 m && eq_user u2 u then t else Cons (m2,u2) (leaveMeet_ m u t)
    end
      
    
  let leave (m:meeting) (u:user) (state:stateT) : ()
    writes{state.participants, state.participants_}
    requires{not Fset.mem (m,u) state.host}
    requires{Fset.mem m state.active}
    requires{Fset.mem (m,u) state.participants} 
    ensures{state.participants = Fset.remove (m,u) (old state.participants)}
    ensures{state.host = old state.host}
    ensures{state.active = old state.active}
  = 
    state.participants <- Fset.remove (m, u) state.participants;
    state.participants_ <- leaveMeet_ m u state.participants_  

    
   
    
  let rec function remove_meeting_participants (m : meeting) (l:list (meeting,user)) : list (meeting,user)
    ensures{forall u:user, m2:meeting. m<>m2 -> (num_occ (m2,u) result = num_occ (m2,u) l)}
    ensures{forall u:user. not Mem.mem (m,u) result}
    ensures{forall u:user, m2:meeting. m<>m2 -> (Mem.mem (m2,u) l <-> Mem.mem (m2,u) result)}
    ensures{forall u:user. not Mem.mem (m,u) result}
    variant{l}
    =
    match l with
    | Nil -> Nil
    | Cons (m2,u2) t -> if eq_meeting m2 m then remove_meeting_participants m t else Cons (m2,u2) (remove_meeting_participants m t)
    end



  let rec function deactivate_meeting_ (m:meeting) (l:list meeting) : list meeting
    requires{num_occ m l = 1}
    ensures{forall m2:meeting. (m2<>m) -> (num_occ m2 l = num_occ m2 result)}
    ensures{not Mem.mem m result}
    =
    match l with
    | Nil -> Nil
    | Cons m2 t -> if eq_meeting m2 m then t else Cons m2 (deactivate_meeting_ m t)
    end
   
    
    
  let end_ (m:meeting) (state:stateT) : ()
    writes{state.participants, state.participants_, state.active, state.active_}
    requires{mem m state.active}
    ensures{state.host = old state.host}
    ensures{state.active = remove m (old state.active)}
    ensures{forall u:user, m2:meeting. m<>m2 -> (Fset.mem (m2,u) (old state.participants) <-> Fset.mem (m2,u) state.participants)}
    ensures{forall u:user. not Fset.mem (m,u) state.participants}
    =
    state.participants <- elements (remove_meeting_participants m state.participants_);
    state.participants_ <- remove_meeting_participants m state.participants_;  
    state.active <- remove m state.active;
    state.active_ <- deactivate_meeting_ m state.active_
    
     



  clone MeetingAbs with 
    type user, type meeting, type stateT,
    val schedule, val start, val join, val leave, val end_

end




module MeetingImpListEx 
  
  use int.Int
  
  clone MeetingImpList with
    type user=int, type meeting=int,
    val eq_user = (=), val eq_meeting = (=)
    
    
  let main()=
    
    let meet = mkMeeting () in  
    schedule 1 1 meet;
    schedule 2 1 meet;
    start 1 meet;
    start 2 meet;
    schedule 3 2 meet;
    start 3 meet;
    join 3 1 meet;
    join 2 3 meet;
    join 3 3 meet;
    join 3 4 meet;
    end_ 1 meet;
    leave 3 1 meet;
    schedule 4 5 meet;
   
    
    
end














